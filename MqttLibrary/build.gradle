// Build script for the Craxiom MQTT Connection library
//
// The user must have run the following commands to be able to publish a version to Sonatype Maven Central:
//   gradle addCredentials --key sonatypeUsername --value <username>
//   gradle addCredentials --key sonatypePassword --value <password>
//   gradle addCredentials --key sonatypeKeyPassword --value <password>
//
// You also need to have gpg installed for the signing process to work
//
// From there, execute `./gradlew clean build publishReleasePublicationToMavenRepository` to build, sign, and upload all the artifacts.
// The artifacts are then uploaded to the Central Publisher Portal via the OSSRH Staging API.
// The final step is to log in to https://central.sonatype.com/publishing/deployments to publish the validated deployment

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'nu.studer.credentials' version '3.0'
}

group 'com.craxiom'
version "1.0-SNAPSHOT"

android {
    compileSdk 36

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 13
        versionName version

        setProperty("archivesBaseName", "$applicationName-$versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    namespace 'com.craxiom.mqttlibrary'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        disable += ['AllowBackup', 'GoogleAppIndexingWarning', 'MissingApplicationIcon']
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    packaging {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
        singleVariant("debug") {
            withSourcesJar()
        }
    }
}

ext {
    sonatypeUsername = findCredentialValue('Username')
    sonatypePassword = findCredentialValue('Password')
    ext."signing.password" = findCredentialValue('KeyPassword')
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    compileOnly 'com.google.android.things:androidthings:1.0'

    implementation 'com.hivemq:hivemq-mqtt-client:1.3.12'

    implementation "com.google.protobuf:protobuf-java-util:4.33.2"
    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

// Configure publishing to the Sonatype Maven Central repo
// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifactId 'mqtt-library'
                //artifact publishArtifact

                pom {
                    name = 'MQTT Connection Library'
                    description = 'An MQTT UI and connection controller that can be used in an Android app'
                    url = 'https://github.com/christianrowlands/android-mqtt-connection-lib'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://github.com/christianrowlands/android-mqtt-connection-lib/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'christianrowlands'
                            name = 'Christian Rowlands'
                            email = 'christian.rowlands@gmail.com'
                        }
                        developer {
                            id = 'eliza-mae-alcaraz'
                            name = 'Eliza Alcaraz'
                        }
                    }

                    scm {
                        url = 'https://github.com/christianrowlands/android-mqtt-connection-lib'
                        connection = 'scm:git:git://github.com/christianrowlands/android-mqtt-connection-lib.git'
                        developerConnection = 'scm:git:ssh://git@github.com:christianrowlands/android-mqtt-connection-lib.git'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/christianrowlands/android-mqtt-connection-lib/issues'
                    }

/*                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')

                        // Iterate over the api dependencies to add a dependency node for each
                        configurations.api.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }*/
                }
            }

            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                artifactId = 'mqtt-library-debug'
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
            mavenLocal()
        }
    }
}

signing {
    sign publishing.publications
}

/**
 * Try to locate a credential value (username or password) in the credentials store.
 *
 * @param value The suffix to use for the credential lookup key.  It will be appended to "sonatype".
 * @return The credential value if located.
 */
private String findCredentialValue(String value) {
    return project.credentials.forKey("sonatype$value")
}
